plugins {
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    defaultConfig {
        applicationId "com.savvasdalkitsis.uhuruphotos"
        minSdk 23
        targetSdk 32
        versionCode project.app_version_code
        versionName "${project.app_version_name}"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file("keystore/keystore.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASS")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'com.savvasdalkitsis.uhuruphotos'
}

dependencies {
    implementation project(":auth")
    implementation project(":feedpage")
    implementation project(":home")
    implementation project(":navigation")
    implementation project(":ui")
    implementation project(":server")
    implementation project(":api:initializer")
    implementation project(':api:userbadge')
    implementation project(":api:homenavigation")

    // internal projects
    implementation project(":internal:albums")
    implementation project(":internal:heatmap")
    implementation project(":internal:library")
    implementation project(":internal:log")
    implementation project(":internal:notification")
    implementation project(":internal:person")
    implementation project(":internal:people")
    implementation project(":internal:photos")
    implementation project(":internal:search")
    implementation project(":internal:settings")
    implementation project(":internal:share")
    implementation project(":internal:user")
    implementation project(":internal:userbadge")
    implementation project(":internal:video")

    implementation(
            libs.accompanist.systemui,
            libs.androidx.activity,
            libs.androidx.compose.hilt.navigation,
            libs.androidx.compose.material,
            libs.androidx.compose.material.windowsize,
            libs.androidx.compose.navigation,
            libs.androidx.compose.ui,
            libs.androidx.datastore.preferences,
            libs.androidx.lifecycle,
            libs.androidx.preference,
            libs.androidx.work,
            libs.androidx.work.hilt,
            libs.flowpreferences,
            libs.hilt,
    )
    implementation platform(libs.firebase.bom)
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
}