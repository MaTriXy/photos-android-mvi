plugins {
    id 'dagger.hilt.android.plugin'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    defaultConfig {
        applicationId "com.savvasdalkitsis.uhuruphotos"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            def workspacePath = System.getProperty("user.home") + "/work/_temp/keystore/"
            def allFiles = new File(workspacePath).listFiles()

            if (allFiles != null) {
                def keystoreFile = allFiles.first()
                keystoreFile.renameTo("app/keystore/keystore.jks")
            }
            storeFile file("keystore/keystore.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("KEYSTORE_ALIAS")
            keyPassword System.getenv("KEYSTORE_PASS")
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation project(":auth")
    implementation project(":feedpage")
    implementation project(":home")
    implementation project(":navigation")
    implementation project(":photos")
    implementation project(":ui")
    implementation project(":search")
    implementation project(":server")
    implementation project(":initializer")
    implementation project(":video")
    implementation project(":settings")
    implementation project(":userbadge-api")
    implementation project(":userbadge")
    implementation(
            libs.accompanist.systemui,
            libs.androidx.activity,
            libs.androidx.compose.hilt.navigation,
            libs.androidx.compose.material,
            libs.androidx.compose.navigation,
            libs.androidx.compose.ui,
            libs.androidx.datastore.preferences,
            libs.androidx.lifecycle,
            libs.androidx.preference,
            libs.androidx.work,
            libs.androidx.work.hilt,
            libs.flowpreferences,
            libs.hilt,
    )
}